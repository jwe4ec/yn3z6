# Lagged mediation imputation model based on average item scores with prDoSS (diagnostic script)
# Author: Jeremy W. Eberle

# This script uses the standalone software Blimp Base (ver. 3.0.58) and Blimp Studio 
# (ver. 1.3.5) to conduct Bayesian estimation

# Start by determining number of burn-in iterations needed for convergence. The model 
# converges (i.e., PSR < 1.05) after 13,000 iterations.

# Note: The code below does not save trace plots. The plots have to be manually 
# saved from within Blimp Studio.

DATA: C:\Users\Jeremy\Box Sync\paper w Kristin\rerunning analyses\For OSF\data\intermediate\contemp_aux_med.csv;
VARIABLES: ResearchID time0 Condition AIN Period 
           meanDSS drtotl_m_imp cnDoSS KMTOT
           DDS14_factor DDS17a2_factor prDoSS DDS17a2_factor_collapsed DDS17a2_factor_collapsed2 cond0rev
           drtotl_m_imp_pomp meanDSS_pomp cnDoSS_pomp KMTOT_pomp;
ORDINAL: cond0rev;
NOMINAL: DDS14_factor DDS17a2_factor_collapsed2;
MISSING: 999;
TRANSFORM: lmeanDSS = lag1(meanDSS, time0, ResearchID);
           lKMTOT = lag1(KMTOT, time0, ResearchID);
           lcnDoSS = lag1(cnDoSS, time0, ResearchID);

CLUSTERID: ResearchID;

FIXED: DDS14_factor DDS17a2_factor_collapsed2 cond0rev;

MODEL: 
lmeanDSS ~ cond0rev@a1path | 1;
lKMTOT ~ cond0rev@a2path | 1;
lcnDoSS ~ cond0rev@a3path | 1;
drtotl_m_imp ~ lmeanDSS lmeanDSS.mean@b1path lKMTOT lKMTOT.mean@b2path lcnDoSS lcnDoSS.mean@b3path cond0rev@direct DDS14_factor DDS17a2_factor_collapsed2
                        | lmeanDSS lKMTOT lcnDoSS;
prDoSS ~ drtotl_m_imp lmeanDSS lmeanDSS.mean lKMTOT lKMTOT.mean lcnDoSS lcnDoSS.mean cond0rev DDS14_factor DDS17a2_factor_collapsed2
                        | lmeanDSS lKMTOT lcnDoSS;

CENTER: GROUPMEAN = lmeanDSS lKMTOT lcnDoSS, GRANDMEAN = lmeanDSS.mean lKMTOT.mean lcnDoSS.mean;

PARAMETERS:
specific.indirect1 = a1path * b1path;
specific.indirect2 = a2path * b2path;
specific.indirect3 = a3path * b3path;
total.indirect = specific.indirect1 + specific.indirect2 + specific.indirect3;
total = total.indirect + direct;

SEED: 1234;
BURN: 20000;
THIN: 1;
NIMPS: 2; 

OPTIONS: latent hov psr SaveLatent;
SAVE: stacked = C:\Users\Jeremy\Box Sync\paper w Kristin\rerunning analyses\For OSF\data\imputed\mediation\maximal_w_prDoSS\lagged\avg_item\diagnostic\imps.csv,
      burn = C:\Users\Jeremy\Box Sync\paper w Kristin\rerunning analyses\For OSF\data\imputed\mediation\maximal_w_prDoSS\lagged\avg_item\diagnostic\burn*.csv,
      iterations = C:\Users\Jeremy\Box Sync\paper w Kristin\rerunning analyses\For OSF\data\imputed\mediation\maximal_w_prDoSS\lagged\avg_item\diagnostic\iterations*.csv,
      estimates = C:\Users\Jeremy\Box Sync\paper w Kristin\rerunning analyses\For OSF\data\imputed\mediation\maximal_w_prDoSS\lagged\avg_item\diagnostic\estimates.txt,
      starts = C:\Users\Jeremy\Box Sync\paper w Kristin\rerunning analyses\For OSF\data\imputed\mediation\maximal_w_prDoSS\lagged\avg_item\diagnostic\starts.txt,
      psr = C:\Users\Jeremy\Box Sync\paper w Kristin\rerunning analyses\For OSF\data\imputed\mediation\maximal_w_prDoSS\lagged\avg_item\diagnostic\psr.csv;